#version 450
#extension GL_ARB_separate_shader_objects : enable

layout(location = 0) in vec3 inPosition;
layout(location = 1) in vec3 inNormal;
layout(location = 2) in vec2 inTexCoords;
layout(location = 3) in vec3 inTangent;
layout(location = 4) in vec3 inBitangent;
layout(location = 5) in mat4 inModelMatrix;

layout(location = 0) out vec2 TexCoords;
layout(location = 1) out vec3 TangentLightPos;
layout(location = 2) out vec3 TangentLightDir;
layout(location = 3) out vec3 TangentViewPos;
layout(location = 4) out vec3 TangentFragPos;

struct DirLight
{
	vec3 diffuse;

	float specular;
	float intensity;
	
	vec3 ambient;
	vec3 direction;
};

layout(binding = 0) uniform UboScene
{
    mat4 view;
    mat4 proj;
	vec3 viewPos;
} ubo;

layout(binding = 2) uniform Lights
{
	uint lightNum;
	DirLight dirLight;
} lights;

void main() 
{
	mat3 normalMatrix = transpose(inverse(mat3(inModelMatrix)));
	vec3 fragPos = vec3(inModelMatrix * vec4(inPosition, 1.0));
	TexCoords = inTexCoords;
	
    vec3 T = normalize(normalMatrix * inTangent);
    vec3 B = normalize(normalMatrix * inBitangent);
    vec3 N = normalize(normalMatrix * inNormal);
    mat3 TBN = transpose(mat3(T, B, N));
	
    TangentLightPos = TBN * vec3(1.0);
    TangentLightDir = TBN * lights.dirLight.direction;
    TangentViewPos  = TBN * ubo.viewPos;
    TangentFragPos  = TBN * fragPos;
    
    gl_Position = ubo.proj * ubo.view * inModelMatrix * vec4(inPosition, 1.0);
}